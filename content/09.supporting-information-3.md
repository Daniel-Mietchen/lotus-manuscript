### SI 3 - Wikidata SPARQL Queries {.page_break_before}

**TODO** Comment them!

#### Query 1 - *Arabidopsis thaliana*

This query answers to the following question:
`What are the compounds present in Mouse-ear cress (Arabidopsis thaliana)?`
Link: [https://w.wiki/32y8](https://w.wiki/32y8)
```SQL
SELECT DISTINCT ?compound ?compoundLabel WHERE {
  VALUES ?classes {
    wd:Q11173 # chemical compound
    wd:Q59199015 # group of stereoisomers
  }
  ?compound wdt:P31 ?classes.
  ?taxon wdt:P225 "Arabidopsis thaliana".
  {
    ?compound p:P703 ?stmt.
    ?stmt ps:P703 ?taxon;
      prov:wasDerivedFrom ?ref.
    ?ref pr:P248 ?art.
    ?art wdt:P356 ?art_doi.
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
```

#### Query 2 - β-sitosterol planar structure {.page_break_before}

This query answers to the following question:
`Which organisms are known to contain compounds sharing the planar structure of β-sitosterol?`
Link: [https://w.wiki/334q](https://w.wiki/334q)
```SQL
SELECT DISTINCT ?taxon ?taxonname WHERE {
  VALUES ?classes {
    wd:Q11173 # chemical compound
    wd:Q59199015 # group of stereoisomers
  }
  ?compound wdt:P31 ?classes;
    wdt:P235 ?inchikey.
  FILTER(REGEX(?inchikey, "KZJWDPNRJALLNS", "i"))
  {
    ?compound p:P703 ?stmt.
    ?stmt ps:P703 ?taxon.
    {
      ?stmt prov:wasDerivedFrom ?ref.
      ?ref pr:P248 ?art.
      ?art wdt:P356 ?art_doi.
    }
  }
  ?taxon wdt:P225 ?taxonname .
}
```

#### Query 3 - β-sitosterol stereoisomers {.page_break_before}

This query answers to the following question:
`Which organisms are known to contain compounds sharing the planar structure of β-sitosterol?`
Link: [https://w.wiki/334s](https://w.wiki/334s)
```SQL
# taxa with chemical compounds related to (but different from) beta-sitosterol
SELECT ?compound ?compoundLabel ?InChIKey ?taxonname 
WITH {
  SELECT ?compound ?InChIKey WHERE {
    wd:Q121802 wdt:P235 ?queryKey . # beta-sitosterol
    ?compound wdt:P235 ?InChIKey .
    FILTER ( ?InChIKey != ?queryKey )
    FILTER (regex(str(?InChIKey), concat("^", substr($queryKey,1,14), "-")))
  }
} AS %compounds
WHERE {
  INCLUDE %compounds
  ?compound wdt:P703/wdt:P225 ?taxonname .
  ?compound rdfs:label ?compoundLabel.
  FILTER(LANG(?compoundLabel) = "en").
}
ORDER BY ASC(?InChIKey)
}
```

#### Query 4 - Pigments {.page_break_before}

This query answers to the following question:
`Which pigments are found in which taxa, according to which reference?`
Link: [https://w.wiki/38Rt](https://w.wiki/38Rt)
```SQL
# Pigments and the taxa they were found in, along with references
SELECT DISTINCT ?compound ?compoundLabel ?taxon ?taxonname ?DOI 
WITH {
  SELECT ?compound WHERE {
    ?compound wdt:P31*/wdt:P279* wd:Q161179.  # get pigments
  }
} AS %compounds
WITH {
  SELECT ?compound ?P703statement WHERE {
    INCLUDE %compounds                        
            ?compound p:P703 ?P703statement.  # check for "found in taxon" statements
  }
} AS %P703statement
WITH {
  SELECT ?compound ?taxon ?DOI WHERE {
    INCLUDE %P703statement
            ?P703statement ps:P703 ?taxon ;   # get the respective taxa
            prov:wasDerivedFrom / pr:P248 [   # get the reference supporting that statement
              wdt:P356 ?DOI                   # get the DOI for the reference
            ] .
  }
} AS %taxa
WHERE {
  {
    INCLUDE %taxa

            ?taxon wdt:P225 ?taxonname .      # get the taxon name
  }
  ?compound rdfs:label ?compoundLabel .       # get compound labels
  FILTER (LANG(?compoundLabel) = "en") .      # filter for English
}
ORDER BY ASC(?compoundLabel)
LIMIT 1000
```

#### Query 5 - Sister taxon compounds {.page_break_before}

This query answers to the following question:
`What are examples of organisms where compounds were reported to be produced by an organism sharing the same parent taxon, but not the organism itself?`
Link: [https://w.wiki/3359](https://w.wiki/3359)
```SQL
SELECT ?compound ?compoundLabel ?taxonname_with_compound ?taxonname_without_compound

?parent_taxon 

WITH
{ 
  SELECT DISTINCT ?compound ?taxon_with_compound ?parent_taxon 
  WHERE {
    VALUES ?classes {
      wd:Q11173
      wd:Q59199015
    }
    ?compound wdt:P31 ?classes;
              wdt:P235 ?inchikey.
    SERVICE bd:sample { ?compound wdt:P703 ?taxon_with_compound . bd:serviceParam bd:sample.limit 1000 }   
    ?taxon_with_compound wdt:P171 ?parent_taxon .
  }
} AS %taxon_with_compound 
WITH
{ 
  SELECT ?taxon_without_compound ?parent_taxon ?compound 
  WHERE {
    INCLUDE %taxon_with_compound
    ?taxon_without_compound wdt:P171 ?parent_taxon .
    FILTER (?taxon_with_compound != ?taxon_without_compound)
  }
} AS %taxon2 
WHERE {
  INCLUDE %taxon_with_compound
  INCLUDE %taxon2
  FILTER NOT EXISTS {  ?compound wdt:P703 ?taxon_without_compound .}
  ?taxon_with_compound wdt:P225 ?taxonname_with_compound .
  ?taxon_without_compound wdt:P225 ?taxonname_without_compound .
  ?compound rdfs:label ?compoundLabel.
  FILTER(LANG(?compoundLabel) = "en").
}
```

#### Query 6 - *Zephyranthes* sister taxon compounds {.page_break_before}

This query answers to the following question:
`Which *Zephyranthes* species lack compounds known from at least two sister species?`
Link: [https://w.wiki/335x](https://w.wiki/335x)
```SQL
PREFIX target: <http://www.wikidata.org/entity/Q191364> # Zephyranthes
SELECT DISTINCT ?compound ?compoundLabel ?taxon_with_compound ?another_taxon_with_compound ?taxon_without_compound
WITH
{ 
  SELECT DISTINCT ?compound ?taxon_YES_1 ?taxon_YES_2 
  WHERE {
    ?taxon_YES_1 wdt:P171 target: .
    ?taxon_YES_2 wdt:P171 target: .
    FILTER (?taxon_YES_2 != ?taxon_YES_1)
    ?compound wdt:P703 ?taxon_YES_1 . 
    ?compound wdt:P703 ?taxon_YES_2
  }
} AS %taxa_with_compound 
WITH
{ 
  SELECT DISTINCT ?taxon_NO ?compound 
  WHERE {
    INCLUDE %taxa_with_compound
    ?taxon_NO wdt:P171 target: .
    FILTER (?taxon_YES_1 != ?taxon_NO)
  }
} AS %taxon_without_compond 
WHERE {
  INCLUDE %taxa_with_compound
  INCLUDE %taxon_without_compond
  FILTER NOT EXISTS { ?compound wdt:P703 ?taxon_NO .}
  ?taxon_YES_1 wdt:P225 ?taxon_with_compound .
  ?taxon_YES_2 wdt:P225 ?another_taxon_with_compound .
  ?taxon_NO wdt:P225 ?taxon_without_compound .
  ?compound rdfs:label ?compoundLabel.
  FILTER(LANG(?compoundLabel) = "en").
}
```

#### Query 7 - Antibiotic-like compounds {.page_break_before}

This query answers to the following question:
`How many compounds are structurally similar to compounds labeled as antibiotics? Results are grouped by the parent taxon of the organism they were found in.`
Link: [https://w.wiki/32Qb](https://w.wiki/32Qb)
```SQL
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>

SELECT ?parent_taxon ?parent_taxon_name (COUNT(DISTINCT ?compound) AS ?count) WHERE {
  SERVICE idsm:wikidata {
    SERVICE <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
      ?antibiotic wdt:P279* / wdt:P2868 / wdt:P486 "D000900".
      ?antibiotic wdt:P233 ?smiles.
    }
    
    ?compound sachem:similarCompoundSearch [
      sachem:query ?smiles;
      sachem:cutoff "0.9"^^xsd:double ].
  }

  hint:Prior hint:runFirst true. # hint to evaluate the idsm service first 

  ?compound wdt:P703 ?taxon.
  ?taxon wdt:P171 ?parent_taxon.
  OPTIONAL { ?parent_taxon wdt:P225 ?parent_taxon_name. }
}
GROUP BY ?parent_taxon ?parent_taxon_name
ORDER BY DESC (?count)
```

#### Query 8 - Triples {.page_break_before}

This query answers to the following question:
`Which compounds are found in a biological organism, according to which references?`
Link: [https://w.wiki/335C](https://w.wiki/335C)
```SQL
SELECT DISTINCT ?compound ?compound_inchi ?compound_inchikey ?taxon ?taxon_name ?reference ?reference_doi  WHERE {
  ?compound wdt:P235 ?compound_inchikey;
             p:P703 [
               ps:P703 ?taxon ;
                       prov:wasDerivedFrom / pr:P248 [
                         wdt:P356 ?reference_doi
                       ]
             ] .
  ?compound wdt:P234 ?compound_inchi.
  ?taxon wdt:P225 ?taxon_name.
  ?reference wdt:P356 ?reference_doi.
}
LIMIT 1000
```

#### Query 9 - Indolic scaffold {.page_break_before}

This query answers to the following question:
`Which organisms contain indolic scaffold? Results are grouped by the parent taxon and ordered.`
Link: [https://w.wiki/32KZ](https://w.wiki/32KZ)
```SQL
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>

SELECT ?parent_taxon ?parent_taxon_name (COUNT(?compound) AS ?count) WHERE {
  SERVICE idsm:wikidata {
    ?compound sachem:substructureSearch
      [ sachem:query "NCCC1=CNC2=C1C=CC=C2" ]
  }

  hint:Prior hint:runFirst true. # hint to evaluate the idsm service first

  ?compound p:P703 ?statement.
  ?compound wdt:P235 ?inchikey.
  ?statement ps:P703 ?taxon.
  ?taxon wdt:P171 ?parent_taxon.
  OPTIONAL { ?taxon wdt:P171 ?parent_taxon. }
  OPTIONAL { ?taxon wdt:P225 ?taxon_name. }
  OPTIONAL { ?parent_taxon wdt:P225 ?parent_taxon_name. }
  OPTIONAL { ?taxon wdt:P846 ?taxon_id_gbif. }
  OPTIONAL { ?taxon wdt:P685 ?taxon_id_ncbi. }
}
GROUP BY ?parent_taxon ?parent_taxon_name
ORDER BY DESC (?count)
```

#### Query 10 - Senior NP chemists {.page_break_before}

This query answers to the following question:
`How many structure-organism pairs have been referenced by these authors? (Here, two senior natural products chemists and co-authors of this paper are compared to the late Ferdinand Bohlmann).`
Link: [https://w.wiki/32$m](https://w.wiki/32$m)
```SQL
#defaultView:BarChart
SELECT ?authors_namesLabel (COUNT(DISTINCT(?compound)) AS ?count) WHERE {
  VALUES ?classes {
    wd:Q11173 # chemical compound
    wd:Q59199015 # group of stereoisomers
    wd:Q79529 # chemical substance
    wd:Q17339814 # group of chemical substances
    wd:Q47154513 # structural class of chemical compounds
  }
  ?compound wdt:P31 ?classes. # instance of
  #?taxon wdt:P225 'Arabidopsis thaliana'
  {
    ?compound p:P703 ?stmt. # found in taxon
    ?stmt ps:P703 ?taxon. # found in taxon
    ?stmt prov:wasDerivedFrom ?ref.
    ?ref pr:P248 ?art. # stated in
  }
  VALUES ?authors_names {
    wd:Q56084663 # JLW
    wd:Q40259636 # GFP
    wd:Q1405133 # A german chemist of the 20th century ... Ferdinand Bohlmann
  }
  ?art wdt:P50 ?authors_names.
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?authors_namesLabel
ORDER BY DESC (?count)
```
